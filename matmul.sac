#ifndef P
#define P 1000
#endif

#ifndef ITER
#define ITER 100
#endif

inline bool < (int a, int b) { return _lt_SxS_(a, b); }
inline int[.] ++ (int[.] a, int[.] b) { return _cat_VxV_(a, b); }
inline double + (double a, double b) { return _add_SxS_(a, b); }
inline double[.] * (double[.] a, double[.] b) { return _mul_VxV_(a, b); }

inline
double[d:shp] sel(int[n] idx, double[n:_,d:shp] a)
{
    return { iv -> _sel_VxA_(idx ++ iv, a) | iv < shp };
}

inline
double sum(double[n] a)
{
    return with {
        ([0] <= iv < [n]) : a[iv];
    } : fold (+, 0);
}

inline
double[u,w] matmul(double[u,v] a, double[v,w] b)
{
    return { [i,j] -> sum(a[[i]] * b[.,j]) };
}

noinline
double run(int i, double[.,.] a, double[.,.] b)
{
    res = matmul(a, b);
    return res[1,2];
}

int main()
{
    a = { [i,j] -> i | [i,j] < [P,P] };
    b = { [i,j] -> j | [i,j] < [P,P] };

    res = 0.0;
    for (i = 0; i < ITER; i += 1) {
        res += run(i, a, b);
    }

    return res;
}
