inline double[d:shp] sel(int[n] idx, double[n:_,d:shp] a)
{
    return { iv -> _sel_VxA_(_cat_VxV_(idx, iv), a) | iv < shp };
}

inline double +(double a, double b) { return _add_SxS_(a, b); }

inline double sum(double[n] a)
{
    return with {
        ([0] <= iv < [n]): a[iv];
    } : fold(+, 0.0);
}

noinline double[u,w] matmul(double[u,v] a, double[v,w] b)
{
    return { [i,j] -> sum(_mul_VxV_(a[[i]], b[.,j])) };
}

// Just to ensure that the compiler does not optimise anything away
noinline double matmul_(int i, double[.,.] a, double[.,.] b)
{
    res = matmul(a, b);
    return res[[0,0]];
}

noinline double run(int[.,.] sizes, int i)
{
    size = _sel_VxA_([i,0], sizes);
    iter = _sel_VxA_([i,1], sizes);

    a = { [i,j] -> _tod_S_(_add_SxS_(i, j)) | [i,j] < [size,size] };
    b = { [i,j] -> _tod_S_(_sub_SxS_(i, j)) | [i,j] < [size,size] };

    res = 0.0;
    for (j = 0; _le_SxS_(j, iter); j = _add_SxS_(j, 1)) {
        c = matmul_(j, a, b);
        res = _max_SxS_(res, c);
    }

    return res;
}

int main()
{
    SIZES = [
        [500, 270],
        [750, 180],
        [1000, 120],
        [1250, 105],
        [1500, 90],
        [500, 270],
        [750, 180],
        [1000, 120],
        [1250, 105],
        [1500, 90]
    ];
    SIZES = _hideValue_SxA_(0, SIZES);

    res = 0.0;

    len = _sel_VxA_([0], _shape_A_(SIZES));
    for (i = 0; _lt_SxS_(i, len); i = _add_SxS_(i, 1)) {
        res = _max_SxS_(res, run(SIZES, i));
    }

    return _toi_S_(res);
}
