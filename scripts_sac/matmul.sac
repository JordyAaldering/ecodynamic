#ifndef P
#define P 1000
#endif

#ifndef ITER
#define ITER 50
#endif

inline
double +(double a, double b) { return _add_SxS_(a, b); }

inline
double[d:shp] sel(int[n] idx, double[n:_,d:shp] a)
{
    return { iv -> _sel_VxA_(_cat_VxV_(idx, iv), a) | iv < shp };
}

inline
double sum(double[n] a)
{
    return with {
        ([0] <= iv < [n]): a[iv];
    } : fold(+, 0.0);
}

noinline
double[u,w] matmul(double[u,v] a, double[v,w] b)
{
    return { [i,j] -> sum(_mul_VxV_(a[[i]], b[.,j])) };
}

int main()
{
    a = { [i,j] -> _tod_S_(i) | [i,j] < [P,P] };
    b = { [i,j] -> _tod_S_(j) | [i,j] < [P,P] };

    i = 0;
    while (_lt_SxS_(i, ITER)) {
        a = matmul(a, b);
        i = _add_SxS_(i, 1);
    }

    return _toi_S_(sum({ [i] -> sum(a[[i]]) }));
}
